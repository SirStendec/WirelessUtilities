buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

ext.buildnumber = System.getenv("BUILD_NUMBER") ?: "dev"

version = "1.9." + buildnumber
group = "com.lordmau5"
archivesBaseName = "wirelessutils"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config. It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

repositories {
    maven {
        name = "Forge Maven"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {
        name = "JEI Maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "CraftTweaker Maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "Raoulvdberge Maven"
        url = "https://repo.raoulvdberge.com"
    }
    maven {
        name = "mcjty"
        url = "http://maven.k-4u.nl"
    }
    maven {
        name = "tterrag Maven"
        url = "https://maven.tterrag.com/"
    }
    maven {
        name = "tehnut"
        url = "http://tehnut.info/maven"
    }
    maven {
        name = "oc"
        url = "http://maven.cil.li/"
    }
    maven {
        name = "CodeChicken"
        url = "http://chickenbones.net/maven"
    }
}

dependencies {
    deobfCompile "cofh:RedstoneFlux:${config.rf_mc_version}-${config.rf_version}:universal"
    deobfCompile "cofh:CoFHCore:${config.mc_version}-${config.cofh_core_version}:universal"
    deobfProvided "refinedstorage:refinedstorage:${config.rs_version}"
    deobfCompile "appeng:appliedenergistics2:${config.ae2_version}"
    provided "com.github.mcjty:mcjtylib:${config.mcjtylib_version}"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:${config.top_version}"
    deobfProvided "CraftTweaker2:CraftTweaker2-API:${config.ct_api_version}"
    runtime "CraftTweaker2:CraftTweaker2-MC1120-Main:${config.ct_version}"
    deobfCompile "mezz.jei:jei_${config.mc_version}:${config.jei_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${config.hwyla_version}"
    deobfCompile "codechicken:CodeChickenLib:${config.mc_version}-${config.ccl_version}:universal"
}

minecraft {
    version = config.mc_version + "-" + config.forge_version
    runDir = "run"
    mappings = config.mappings
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

version = "${project.minecraft.version}-${project.version}"

jar {
    classifier = 'universal'
    manifest {
        attributes 'FMLAT': 'wirelessutils_at.cfg'
    }
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives srcJar
}